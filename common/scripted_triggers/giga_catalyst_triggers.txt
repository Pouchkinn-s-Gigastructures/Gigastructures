
# is this the catalyst planet
is_giga_catalyst_planet = {
    exists = leader
    leader = {
        is_giga_catalyst_leader = yes
    }
}

# do we have the council position filled, or the tech for gestalts?
# lets the support buildings be built on any planet
giga_catalyst_can_build_anywhere = {
    or = {
        has_councilor = {
            COUNCILOR = giga_councilor_catalyst
        }
    }
}

# the catalyst leader
is_giga_catalyst_leader = {
    always = yes
}

# do we have the leader
has_giga_catalyst_leader = {
    any_owned_leader = {
        is_giga_catalyst_leader = yes
    }
}

# is this planet the catalyst planet, or are we able to build anywhere
giga_catalyst_building_potential = {
    or = {
        is_giga_catalyst_planet = yes
        and = {
            exists = owner
            owner = {
                giga_catalyst_can_build_anywhere = yes
            }
        }
    }
}

# does this planet have the crafter, or are we able to build anywhere and have a crafter *somewhere*
giga_catalyst_building_allow = {
    if = {
        limit = {
            exists = owner
            owner = {
                giga_catalyst_can_build_anywhere = yes
            }
        }
        owner = {
            any_owned_planet = {
                has_building = building_giga_catalyst_crafter
            }
        }
    }
    else = {
        has_building = building_giga_catalyst_crafter
    }
}

# demolish if we no longer have the leader
giga_catalyst_building_demolish_trigger = {
    or = {
        not = { exists = owner }
        owner = { has_giga_catalyst_leader = no }
    }
}